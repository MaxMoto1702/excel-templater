[[styles]]
== Styling Cells

=== Data format

Data format setting as string.

[source,groovy]
----
def builder = new ExcelBuilder()

builder.config {
    // waterfall
    dataFormat('money', '# ##0.00')
    style('style with money format') { cellStyle ->
        cellStyle.dataFormat = dataFormat('money')
    }

    // inline
    style('style with number format') { cellStyle ->
        cellStyle.dataFormat = dataFormat('money', '#0')
    }
}

builder.build {
    sheet {
        row {
            cell(style: 'style with money format') {
                1000.00
            }
            cell(style: 'style with number format') {
                1000
            }
        }
    }
}

workbook.write(someOutputStream)
----

NOTE: Format name can be any object. Example, simple enum value.

=== Font

Fonts are configure via Apache POI API. See list of properties in class
https://poi.apache.org/apidocs/org/apache/poi/xssf/usermodel/XSSFFont.html[XSSFFont]

==== Configure font in waterfall style

[source,groovy]
----
def builder = new ExcelBuilder()

builder.config {
    font('configured font') { font ->
        font.fontName = 'Arial'
    }
    style('style with configured font') { cellStyle ->
        cellStyle.font = font('configured font')
    }
}

builder.build {
    sheet {
        row {
            cell(style: 'style with configured font') {
                "test"
            }
        }
    }
}

workbook.write(someOutputStream)
----

NOTE: Font name can be any object. Example, simple enum value.

==== Configure font inline style

[source,groovy]
----
def builder = new ExcelBuilder()

builder.config {
    style('style with configured font') { cellStyle ->
        cellStyle.font = font('configured font') { font ->
            font.fontName = 'Arial'
        }
    }
}

builder.build {
    sheet {
        row {
            cell(style: 'style with configured font') {
                "test"
            }
        }
    }
}

workbook.write(someOutputStream)
----

=== Style

Styles in the builder set up similarly with classes in HTML. First,
configure styles and then apply them to the rows or cells.

Styles are configure via Apache POI API. See list of properties in class
https://poi.apache.org/apidocs/org/apache/poi/xssf/usermodel/XSSFCellStyle.html[XSSFCellStyle]

[source,groovy]
----
def builder = new ExcelBuilder()

builder.config {
    style { cellStyle ->
        // Configure style using Apache POI API
        cellStyle.verticalAlignment = CellStyle.VERTICAL_CENTER
        cellStyle.alignment = CellStyle.ALIGN_CENTER
        cellStyle.wrapText = true
        // ...
        return cellStyle
    }
}

builder.build {
    sheet {
        row {
            cell {
                "test"
            }
        }
    }
}

workbook.write(someOutputStream)
----

NOTE: Style name can be any object. Example, simple enum value.